// For bnd
buildscript {
	repositories {
		mavenCentral()
	}
	dependencies {
		classpath 'biz.aQute.bnd:biz.aQute.bnd.gradle:4.1.0'
	}
}

plugins {
	id 'java'
	id 'maven-publish'
	id 'application'
	id 'signing'
}

apply plugin: 'biz.aQute.bnd.builder'

group = 'org.daisy.dotify'

sourceCompatibility = 1.8
targetCompatibility = 1.8

sourceSets {
	main {	java { srcDir 'src' }	resources { srcDir 'src' } }
	test {	java { srcDir 'test' }	resources { srcDir 'test' } }
}

compileJava {
    options.encoding = 'UTF-8'
}

compileTestJava {
	options.encoding = 'UTF-8'
}

def repoRevision = System.getenv("REPO_REVISION")!=null?System.getenv("REPO_REVISION"):repositoryRevision

javadoc {
	options.encoding = 'UTF-8'
	options.source = 8
	options.links('https://docs.oracle.com/javase/8/docs/api/')
}


mainClassName = "org.daisy.dotify.devtools.cli.Main"
startScripts.applicationName = 'dotifydevtools'

repositories {
    mavenCentral()
    //mavenLocal()
    maven { url "https://oss.sonatype.org/content/groups/staging" }
}

dependencies {
    compileOnly 'org.osgi:org.osgi.service.component.annotations:1.3.0'
    compile 'org.daisy.dotify:dotify.api:5.0.0'
    compile 'org.daisy.braille:braille-utils.impl:7.0.0'
    compile 'org.daisy.braille:braille-utils.pef-tools:6.0.0'
    compile 'org.daisy.dotify:dotify.common:4.4.1'

    testImplementation group: "junit", name: "junit", version: "4.12"
}

jar {
	manifest {
		attributes(
			'-sources': 'true',
			'Export-Package':	'org.daisy.dotify.devtools.cli,'+
								'org.daisy.dotify.devtools.converters,'+
								'org.daisy.dotify.devtools.jvm,'+
								'org.daisy.dotify.devtools.regression,'+
								'org.daisy.dotify.devtools.schematron,'+
								'org.daisy.dotify.devtools.unbrailler',
			'Bundle-Name': "$bundleName",
			'Bundle-SymbolicName': "$bundleName",
			'Built-By': System.getProperty("user.name"),
			'Built-On': new Date().format('yyyy-MM-dd'),
			'Repository-Revision': "$repoRevision",
			'Repository-URL': "$repositoryURL",
			'Main-Class': mainClassName,
			'Class-Path': configurations.runtimeClasspath.collect { it.getName() }.join(' '),
			'Include-Resource': 'LICENSE, NOTICE'	
		)
	}
}

task javadocJar(type: Jar) {
    classifier = 'javadoc'
    from javadoc
}

task sourcesJar(type: Jar) {
    classifier = 'sources'
    from sourceSets.main.allSource
}

ext.isReleaseVersion = !version.endsWith("SNAPSHOT")

signing {
	required { isReleaseVersion }
	sign publishing.publications
}

publishing {
	//Only upload if a git hash is supplied. On Travis only upload snapshots.
	//enabled = repoRevision.size()==40 && System.getenv("TRAVIS_BUILD_ID")!=null
	publications {
		maven(MavenPublication) {
			from components.java
			artifact sourcesJar
			artifact javadocJar
			/*
			if (isReleaseVersion) {
				beforeDeployment { MavenDeployment deployment -> signing.signPom(deployment) }
			}*/
			pom {
				name = 'dotify.devtools'
				packaging = 'jar'
				description = 'Dotify Devtools'
				url = "$repositoryURL"
				scm {
					connection = "$repositorySCM"
					developerConnection = "$repositorySCM"
					url = "$repositoryURL"
				}
				licenses {
					license {
						name = 'LGPL'
						url = 'http://www.gnu.org/licenses/lgpl.html'
					}
				}
				developers {
					developer {
						id = 'joel'
						name = 'Joel HÃ¥kansson'
					}
				}
			}
		}
	}
	repositories {
		maven {
			def stagingRepoUrl = "https://oss.sonatype.org/service/local/staging/deploy/maven2/"
			def snapshotsRepoUrl = "https://oss.sonatype.org/content/repositories/snapshots/"
			url = version.endsWith('SNAPSHOT') ? snapshotsRepoUrl : stagingRepoUrl
			credentials {
				username = sonatypeUsername
				password = sonatypePassword
			}
		}
	}
}
wrapper {
	distributionType = Wrapper.DistributionType.ALL
}
